/*

The decimal numeral system is composed of ten digits, which we represent as "0123456789" (the digits in a system are written from lowest to highest). Imagine you have discovered an alien numeral system composed of some number of digits, which may or may not be the same as those used in decimal. For example, if the alien numeral system were represented as "oF8", then the numbers one through ten would be (F, 8, Fo, FF, F8, 8o, 8F, 88, Foo, FoF). We would like to be able to work with numbers in arbitrary alien systems. More generally, we want to be able to convert an arbitrary number that's written in one alien system into a second alien system.


Each language will be represented by a list of its digits, ordered from lowest to highest value. No digit will be repeated in any representation, all digits in the alien number will be present in the source language, and the first digit of the alien number will not be the lowest valued digit of the source language (in other words, the alien numbers have no leading zeroes). Each digit will either be a number 0-9, an uppercase or lowercase letter, or one of the following symbols !"#$%&'()*+,-./:;<=>?@[\]^_`{|}~

Output

The alien number translated from the source language to the target language.

*/


import util.

%Create a SourceMap by matching data in SourceLang to its Value and call convert, which converts first Source -> Decimal -> target
main(AlienNumber,SourceLang,TargetLang) => SourceBase=length(SourceLang),
                                           SourceMap = new_map(),
                                           V1=zip(SourceLang,0..SourceBase-1),
                                           foreach ({E1,E2} in V1)
                                                    SourceMap.put(E1,E2)
                                           end,
                                           convert(AlienNumber,SourceMap,SourceBase,TargetNumber,TargetLang),
                                           writef("Target Number is : "),
                                           writeln(TargetNumber).

%call source_to_decimal and decimal_to_target
convert(AlienNumber,SourceMap,SourceBase,TargetNumber,TargetLang) => source_to_decimal(AlienNumber,SourceMap,SourceBase,Temp),
                                                                     TargetMap=new_map(),
                                                                     TargetBase=length(TargetLang),
                                                                     V2=zip(TargetLang,0..TargetBase-1),
                                                                     foreach ({E1,E2} in V2)
                                                                        TargetMap.put(E2,E1)
                                                                     end,
                                                                     decimal_to_target(Temp,TargetBase,TargetMap,TargetNumber).

%converts source to decimal by SourceMap
source_to_decimal(AlienNumber,SourceMap,SBase,Temp) => sourceHelperUtility(AlienNumber,SBase,SourceMap,0,Temp).

sourceHelperUtility([], SBase, SourceMap, TempSum, Temp) => Temp = TempSum.
sourceHelperUtility([H|T], SBase, SourceMap, TempSum, Temp) => 
                                                sourceHelperUtility(T, SBase, SourceMap, (TempSum*SBase)+SourceMap.get(H), Temp).

%converts decimal to TargetLang by TargetMap
decimal_to_target(0, TargetBase, TargetMap, Num) => Num = [TargetMap.get(0)].
decimal_to_target(Temp, TargetBase, TargetMap, Num) => Ds = [],
                                          %Till Val becomes keep on doing mod and div
                                          while (Temp != 0)
                                            Rem := Temp mod (TargetBase),
                                            Temp := Temp div (TargetBase),
                                            Ds := [TargetMap.get(Rem)|Ds]
                                          end,
                                          Num = Ds.